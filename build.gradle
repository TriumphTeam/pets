plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

allprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'

    group = 'me.mattstudios'
    version = '1.0.0'

    repositories {
        mavenCentral()
        mavenLocal()

        maven {
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }
        maven {
            url = 'https://oss.sonatype.org/content/groups/public/'
        }

    }

    dependencies {

        compileOnly "org.spigotmc:spigot-api:+"

        compileOnly "org.xerial:sqlite-jdbc:3.30.1"

        // Matt stuff
        implementation "me.mattstudios.utils:matt-framework:+"
        implementation "me.mattstudios.utils:matt-framework-gui:1.1.1-BETA"
        implementation "me.mattstudios.utils:matt-core:+"

        // Config repository
        implementation ("ch.jalu:configme:1.1.0") {
            exclude group: "org.yaml", module: "snakeyaml"
        }

        // Kotlin
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    compileJava {
        options.compilerArgs += ["-parameters"]
        options.fork = true
        options.forkOptions.executable = 'javac'
    }

    compileKotlin {
        kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
        kotlinOptions.javaParameters = true
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = JavaVersion.VERSION_1_8
    }
}

apply plugin: 'com.github.johnrengelman.shadow'

shadowJar {
    minimize()

    relocate("me.mattstudios.mf", "me.mattstudios.triumphpets.mf")
    relocate("me.mattstudios.mattcore", "me.mattstudios.triumphpets.core")
    relocate("ch.jalu.configme", "me.mattstudios.triumphpets.configme")

    archiveFileName = "TriumphPets-${project.version}.jar"

    // Testing only
    destinationDir file("T:/servers/spigot-1.15/plugins");
}

dependencies {
    implementation project(":common")
    implementation project(":version1_15")
}

import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}